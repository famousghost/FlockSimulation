// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel FlockSimulation

struct Boid
{
    float4 worldPosition; // pos x, y, z | radius w
    float4 forward; // dir x, y, z | rayLenght w
    float4 velocity; // velocity x y z | debug
    float4 acceleration;
    float4 wallAvoidanceDebug;
    float4x4 localToWorldMatrix;
};

struct Obstacles
{
    float4 worldPosition; //x - posX, y - posY, z - posZ, w type
    float4 worldSize;
};

struct AABB
{
    float3 minVert;
    float3 maxVert;
};

RWStructuredBuffer<Boid> _Boids;
StructuredBuffer<Obstacles> _Obstacles;
StructuredBuffer<float4> _AvoidancePoints;
int _BoidsAmount;
int _ObstacleAmount;
int _AvoidancePointAmount;

cbuffer FlockData
{
    float CohesionStrength;
    float SeparationStrength;
    float AlignmentStrength;
    float WallAvoidanceStrength;
    float MaxAngle;
    float MaxLength;
};

struct BoxHit
{
    float3 hitPoint;
    float depth;
    bool hit;
};

AABB CretaeAABBFromBox(Obstacles obstacle)
{
    AABB aabb;
    aabb.minVert = obstacle.worldPosition.xyz - obstacle.worldSize.xyz * 0.5f;
    aabb.maxVert = obstacle.worldPosition.xyz + obstacle.worldSize.xyz * 0.5f;
    return aabb;
}

BoxHit BoxRaycast(Boid boid, float4 avoidancePoint, Obstacles obstacle)
{
    BoxHit boxHit;
    boxHit.hit = false;
    boxHit.hitPoint = float3(0.0f, 0.0f, 0.0f);
    boxHit.depth = 0.0f;
    
    const float3 avoidancePointWS = mul(boid.localToWorldMatrix, avoidancePoint).xyz;

    const float3 worldDir = normalize(avoidancePointWS - boid.worldPosition.xyz);
    if (acos(dot(worldDir, boid.forward.xyz)) < radians(MaxAngle))
    {
        return boxHit;
    }
    AABB aabb = CretaeAABBFromBox(obstacle);
    const float3 pMin = (aabb.minVert - boid.worldPosition.xyz) / worldDir;
    const float3 pMax = (aabb.maxVert - boid.worldPosition.xyz) / worldDir;

    const float3 tMin = float3(min(pMin.x, pMax.x), min(pMin.y, pMax.y), min(pMin.z, pMax.z));
    const float3 tMax = float3(max(pMin.x, pMax.x), max(pMin.y, pMax.y), max(pMin.z, pMax.z));

    const float maxMin = max(max(tMin.x, tMin.y), tMin.z);

    const float minMax = min(min(tMax.x, tMax.y), tMax.z);

    if (minMax <= 0.0f)
    { 
        return boxHit;
    }

    if (minMax <= maxMin)
    {
        return boxHit;
    }
    if (maxMin <= 0.0f)
    {
        if (minMax > MaxLength)
        {
            return boxHit;
        }
        boxHit.hit = true;
        boxHit.hitPoint = boid.worldPosition.xyz + worldDir * minMax;
        boxHit.depth = minMax;
        return boxHit;
    }

    if (maxMin > MaxLength)
    {
        return boxHit;
    }
    boxHit.hit = true;
    boxHit.hitPoint = boid.worldPosition.xyz + worldDir * maxMin;
    boxHit.depth = maxMin;
    return boxHit;
    
}

float3 WallAvoidance(Boid boid)
{
    float3 force = float3(0.0f, 0.0f, 0.0f);
    for (int i = 0; i < _AvoidancePointAmount; ++i)
    {
        for (int j = 0; j < _ObstacleAmount; ++j)
        {
            BoxHit boxHit = BoxRaycast(boid, _AvoidancePoints[i], _Obstacles[j]);
            if (boxHit.hit)
            {
                force = force + normalize(boid.worldPosition.xyz - boxHit.hitPoint.xyz) / (boxHit.depth * boxHit.depth + 1.0f);
            }
        }
    }
    return force;
}

bool IsClosestBoid(Boid currentBoid, Boid otherBoid)
{
    float3 dir = currentBoid.worldPosition.xyz - otherBoid.worldPosition.xyz;
    if (dot(dir, dir) > (currentBoid.worldPosition.a * currentBoid.worldPosition.a))
    {
        return false;
    }
    return true;
}

float3 CohesionRule(Boid otherBoid)
{
    return otherBoid.worldPosition.xyz;
}

float3 SepeationRule(Boid currentBoid, Boid otherBoid)
{
    return normalize(currentBoid.worldPosition.xyz - otherBoid.worldPosition.xyz);
}

float3 AlignmentRule(Boid otherBoid)
{
    return otherBoid.velocity.xyz;

}

[numthreads(8,1,1)]
void FlockSimulation(uint3 id : SV_DispatchThreadID)
{
    float3 cohesion = float3(0.0f, 0.0f, 0.0f);
    float3 separationDir = float3(0.0f, 0.0f, 0.0f);
    float3 alignment = float3(0.0f, 0.0f, 0.0f);
    float3 acceleration = float3(0.0f, 0.0f, 0.0f);
    int amount = 0;
    for (int i = 0; i < _BoidsAmount; ++i)
    {
        if (!IsClosestBoid(_Boids[id.x], _Boids[i]) || id.x == i)
        {
            continue;
        }
        cohesion += CohesionRule(_Boids[i]);
        separationDir += SepeationRule(_Boids[id.x], _Boids[i]);
        alignment += AlignmentRule(_Boids[i]);
        amount++;
    }
    if(amount > 0)
    {
        acceleration += normalize(alignment / float(amount)) * AlignmentStrength;
        acceleration += normalize((cohesion / float(amount)) - _Boids[id.x].worldPosition.xyz) * CohesionStrength;
        acceleration += normalize((separationDir / float(amount))) * SeparationStrength;
    }
    acceleration = acceleration + WallAvoidance(_Boids[id.x]) * WallAvoidanceStrength;
    _Boids[id.x].acceleration.xyz = acceleration;
}
